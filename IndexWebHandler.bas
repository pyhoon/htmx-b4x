B4J=true
Group=Handlers
ModulesStructureVersion=1
Type=Class
Version=10
@EndOfDesignText@
'Web Handler class
'Version 3.20
Sub Class_Globals
	Private Request As ServletRequest
	Private Response As ServletResponse
	Private Method As String
	Private Elements() As String
	Private ElementKey As String
End Sub

Public Sub Initialize
	
End Sub

Sub Handle (req As ServletRequest, resp As ServletResponse)
	Request = req
	Response = resp
	Method = Request.Method.ToUpperCase
	Dim FullElements() As String = WebApiUtils.GetUriElements(Request.RequestURI)
	Elements = WebApiUtils.CropElements(FullElements, 1)
	If Method <> "GET" Then
		WebApiUtils.ReturnHtmlMethodNotAllowed(Response)
		Return
	End If
	If ElementMatch("") Then
		ShowIndexPage
		Return
	End If
	If ElementMatch("key") Then
		Select ElementKey
			Case "modal"
				WebApiUtils.ReturnHtml(GenerateModal, Response)
				Return
		End Select
	End If
	WebApiUtils.ReturnHtmlPageNotFound(Response)
End Sub

Private Sub ElementMatch (Pattern As String) As Boolean
	Select Pattern
		Case ""
			If Elements.Length = 0 Then
				Return True
			End If
		Case "key"
			If Elements.Length = 1 Then
				ElementKey = Elements(0)
				Return True
			End If
	End Select
	Return False
End Sub

Private Sub ShowIndexPage
	Dim doc As Document
	doc.Initialize
	doc.AppendDocType
	doc.Append(GenerateIndex)
	Dim content As String = doc.ToString
	LogColor(content, -16776961)
	WebApiUtils.ReturnHTML(content, Response)
End Sub

Sub GenerateIndex As String
	Dim html1 As Tag = Html.lang("en")
	html1.comment(" generated by MiniHTML ")
	Head.up(html1).Meta_Preset.Title("HTMX Demo") _
	.Link(CreateMap("href": "https://cdn.jsdelivr.net/npm/bootstrap@5.0.2/dist/css/bootstrap.min.css", "rel": "stylesheet", "integrity": "sha384-EVSTQN3/azprG1Anm3QDgpJLIm9Nao0Yz1ztcQTwFspd3yD65VohhpuuCOmLASjC", "crossorigin": "anonymous")) _
	.Script2("https://cdn.jsdelivr.net/npm/bootstrap@5.0.2/dist/js/bootstrap.bundle.min.js", CreateMap("integrity": "sha384-MrcW6ZMFYlzcLA8Nl+NtUVF0sA7MsXsP1UyJoMp4YLEuNSfAP+JcXn/tWtIaxVXM", "crossorigin": "anonymous")) _
	.Script("assets/js/htmx.min.js")
	Dim body1 As Tag = Body.up(html1)
	Dim div1 As Tag = Div.attribute2(CreateMap("class": "d-flex align-items-center", "style": "height: 100vh")).up(body1)
	Dim div2 As Tag = Div.attribute2(CreateMap("style": "margin: auto")).up(div1)
	
	Dim div3 As Tag = Div.attribute2(CreateMap("id": "modals-here", _
	"class": "modal modal-blur fade", _
	"style" : "display: none", _
	"aria-hidden": "false", _
	"tabindex": "-1")).up(div2)
	Dim div4 As Tag = Div.attribute2(CreateMap("class": "modal-dialog modal-lg modal-dialog-centered", _
	"role": "document")).up(div3)
	Div.attribute("class", "modal-content").up(div4)
	
	Button.attribute2(CreateMap("hx-get": "/modal", _
	"hx-target": "#modals-here", _
	"hx-trigger": "click", _
	"data-bs-toggle": "modal", _
	"data-bs-target": "#modals-here", _
	"class": "btn btn-primary text-uppercase")) _
	.Text("Open Modal").up(div2).uniline
	
	Return html1.Build(-1)
End Sub

Sub GenerateModal As String
	Dim div4 As Tag = Div.attribute("class", "modal-dialog modal-lg modal-dialog-centered")
	Dim div5 As Tag = Div.attribute("class", "modal-content").up(div4)
	Dim div6 As Tag = Div.attribute("class", "modal-header").up(div5)
	H5.attribute("class", "modal-title").Text("Modal title").up(div6)
	Dim div6 As Tag = Div.attribute("class", "modal-body").up(div5)
	p.Text("Modal body text goes here.").uniline.up(div6)
	Dim div6 As Tag = Div.attribute("class", "modal-footer").up(div5)
	Button.attribute2(CreateMap("type": "button", _
	"class": "btn btn-secondary text-uppercase", _
	"data-bs-dismiss": "modal")) _
	.Text("Close").up(div6).uniline
	Return div4.Build(-1)
End Sub